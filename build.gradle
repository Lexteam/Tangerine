plugins {
    id 'java'
    id 'maven'

    id 'idea'
    id 'eclipse'

    id 'net.minecrell.licenser' version '0.1.2'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}
defaultTasks 'clean', 'licenseFormat'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'xyz.lexteam'
version '1.0.1-SNAPSHOT'
description = "Tangerine - Modular Discord bot framework."
ext.url = 'https://www.lexteam.xyz/'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://repo.lexteam.xyz/maven/releases/'
    }
    maven {
        url 'http://repo.lexteam.xyz/maven/snapshots/'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'http://maven.sk89q.com/repo/'
    }
}

dependencies {
    compile 'com.github.austinv11:Discord4j:2.4.4'
    compile 'xyz.lexteam:eventbus:1.5'
    compile 'xyz.lexteam:spectre:1.0.0-SNAPSHOT'
    compile 'com.google.inject:guice:4.0'
    compile 'org.slf4j:slf4j-simple:1.7.18'
    compile 'com.sk89q:intake:3.1.2'
    compile 'com.beust:jcommander:1.48'
}

license {
    header file('HEADER.txt')
    include '**/*.java'
    newLine = false
}

jar {
    classifier = 'base'

    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Main-Class': 'xyz.lexteam.tangerine.Main'
        )
    }
}

shadowJar {
    classifier = ''
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives shadowJar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (System.getenv('MAVEN_RELEASES') != null) {
                repository(url: 'file://' + System.getenv('MAVEN_RELEASES'))
                snapshotRepository(url: 'file://' + System.getenv('MAVEN_SNAPSHOTS'))
            }

            pom {
                artifactId = project.archivesBaseName.toLowerCase()

                project {
                    name = project.archivesBaseName
                    description = project.description
                    packaging = 'jar'
                    url = project.url

                    scm {
                        url = "https://github.com/Lexteam/Tangerine"
                        connection = "scm:git:https://github.com/Lexteam/Tangerine.git"
                        developerConnection = "scm:git:git@github.com:Lexteam/Tangerine.git"
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/Lexteam/Tangerine/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
